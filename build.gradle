plugins {
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'com.github.hierynomus.license' version '0.16.1'
}

group = 'org.openjfx'
version = '0.1.1-SNAPSHOT'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(11)
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation 'com.google.gradle:osdetector-gradle-plugin:1.7.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        javafxPlugin {
            id = 'org.openjfx.javafxplugin'
            displayName = 'JavaFX Gradle Plugin'
            description = 'Plugin that makes it easy to work with JavaFX'
            implementationClass = 'org.openjfx.gradle.JavaFXPlugin'
            website = 'https://github.com/openjfx/javafx-gradle-plugin'
            vcsUrl = 'https://github.com/openjfx/javafx-gradle-plugin'
            tags.set(['java', 'javafx'])
        }
    }
}

publishing {
    repositories {
        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            def sonatypeUsername = providers.gradleProperty('sonatypeUsername')
            def sonatypePassword = providers.gradleProperty('sonatypePassword')
            if (sonatypeUsername.isPresent() && sonatypePassword.isPresent()) {
                credentials {
                    username = sonatypeUsername.get()
                    password = sonatypePassword.get()
                }
            }
        }
    }
}

license {
    skipExistingHeaders = true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
