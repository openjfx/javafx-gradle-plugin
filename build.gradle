plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'maven-publish'
}

group 'org.openjfx'
version '0.0.6-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
    implementation gradleApi()

    implementation 'com.google.gradle:osdetector-gradle-plugin:1.6.0'
    implementation 'org.javamodularity:moduleplugin:1.3.0'

    testImplementation gradleTestKit()
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        javafxPlugin {
            id = 'org.openjfx.javafxplugin'
            displayName = 'JavaFX Gradle Plugin'
            description = 'Plugin that makes it easy to work with JavaFX'
            implementationClass = 'org.openjfx.gradle.JavaFXPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/openjfx/javafx-gradle-plugin'
    vcsUrl = 'https://github.com/openjfx/javafx-gradle-plugin'
    tags = [ 'java', 'javafx' ]
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'javafx-gradle-plugin'
            version = project.version
        }
    }
}
